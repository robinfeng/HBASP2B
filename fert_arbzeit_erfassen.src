// Time receipt customise
// robin.feng@tech-sonic.net
// 2014.7.4
windowevent itemchange barcode
// Disable the "resource" field
item.aplatz_id=protect=1
item.aplatz_id_p=protect=1
item.aplatz_id_p.visible=0
// Disable the "cost element" field
item.timetype_id=protect=1
item.timetype_id_p.visible=0
// Add dropdown list to "serial number" field
item.udf1.dropdown.activate
//item.udf1.edit.update=no

// Control "time type" field, 
// check the setup time and process time in routing:
// If setup time larger than zero, then "time type" equal "setup" 
sql=select "TRAPlatz", "TEAPlatz" into setuptime:#0.000, processtime:#0.000 from beas_ftapl &
  where nummer=<dw_1.item.barcode.value,#0>

if <setuptime> n> 0 then
setitem=typ=R
else
setitem=typ=A
end if

// Update "cost element" by "time type"
setvar=wert1=
if <typ> = H then
  select "thtimetype_id" into timetype_id from "BEAS_FTAPL" &
    where "nummer"=<dw_1.item.barcode.value,#0>
end if
if <typ> = N then
  select "tntimetype_id" into timetype_id from "BEAS_FTAPL" &
    where "nummer"=<dw_1.item.barcode.value,#0>
end if
if <typ> = R then
  select "trtimetype_id" into timetype_id from "BEAS_FTAPL" &
    where "nummer"=<dw_1.item.barcode.value,#0>
end if
if <typ> = A or <typ> = E  then
  select "timetype_id" into timetype_id from "BEAS_FTAPL" &
    where "nummer"=<dw_1.item.barcode.value,#0>
end if
setitem=udf1=
menu=redraw
end event

// Update the dropdown list of Serial Number, when click the dropdown button
windowevent click udf1_p
item.udf1.dropdown.select item from beas_ftpos_reservierung &
  where belnr_id=<dw_1.item.belnr_id.value,#0> and belpos_id=<dw_1.item.belpos_id.value,#0>
end event

// When click Partial receipt button
// Check the serial number and resource
windowevent click b_3
setvar=wert1=
select isnull(serialnumtag,'N') from beas_aplatz where aplatz_id=N'<dw_1.item.aplatz_id.value>'
setvar=res_sn_tag=<wert1>
setvar=sn_len=%len(<dw_1.item.udf1.value>)
setvar=wert1=
select count(1) from beas_ftpos_reservierung &
  where belnr_id=<dw_1.item.belnr_id.value,#0> and belpos_id=<dw_1.item.belpos_id.value,#0> &
  and item='<dw_1.item.udf1.value>'
setvar=sn_in_reservation=<wert1>
//messagebox=<inputserialnumber>
if <res_sn_tag> = 'Y' and <sn_len> n= 0 then
message=error$beas_tr_1001$The resource have to record serial number in time receipt, please input Serial Number
return false
end if

if <res_sn_tag> = 'Y' and <sn_in_reservation> n= 0 then
message=error$beas_tr_1002$The Serial Number is error, can't find the s/n in current work order s/n reservation list.
return false
end if
end event

// When click confirm completion button
// Check the serial number and resource
windowevent click b_4
setvar=wert1=
select isnull(serialnumtag,'N') from beas_aplatz where aplatz_id=N'<dw_1.item.aplatz_id.value>'
setvar=res_sn_tag=<wert1>
setvar=sn_len=%len(<dw_1.item.udf1.value>)
setvar=wert1=
select count(1) from beas_ftpos_reservierung &
  where belnr_id=<dw_1.item.belnr_id.value,#0> and belpos_id=<dw_1.item.belpos_id.value,#0> &
  and item='<dw_1.item.udf1.value>'
setvar=sn_in_reservation=<wert1>
//messagebox=<inputserialnumber>
if <res_sn_tag> = 'Y' and <sn_len> n= 0 then
message=error$beas_tr_1001$The resource have to record serial number in time receipt, please input Serial Number
return false
end if

if <res_sn_tag> = 'Y' and <sn_in_reservation> n= 0 then
message=error$beas_tr_1002$The Serial Number is error, can't find the s/n in current work order s/n reservation list.
return false
end if
end event