// @file: fert_arbzeit_erfassen
// @author: Robin Feng
// @remarks: 
// @version: beas 9.0-000-002-036
// @path: BEAS > Production > Production Time Receipt
// Modified: 2014.7 - 2014.10
// be.as Production Time Receipt window will tigger this customize code.
// Copyright(C) TechSonic
// All rights reserved.

// @eventid: beas_fert_arbzeit_erfassen_open
// @description: Rename the fields on this screen.
windowevent open
[automaticscript]
dw_1.item.menge_gut_rm_t.formatstring=text=Quantity,
[/automaticscript]
end event

// @eventid: beas_fert_arbzeit_erfassen_postopen
// @description: Tigger this event when open this screen
// 1. Disable 2 buttons "partial receipt" and "confirm completetion", when open from work order structure
// 2. Customization authrization of beas
// 3. Disable update "resource" field
// 4. Disable update "cost element" field
windowevent postopen
// @codeid: beas_fert_arbzeit_erfassen_postopen_1
// @description: Disable 2 buttons "partial receipt" and "confirm completetion", when open from work order structure
if <ctyp> = fa then
//  messagebox=error$id=beas_tr_1008<tab>&
//  text=Disable "create time receipt" on this screen! <cr_lf> &
//  Enable "update time receipt" on this screen! <tab>&
//  popup=true
  menu=disable=Teilmelden
  menu=disable=Fertigmelden
  menu=disable=Aus&schuss
end if

// @codeid: beas_fert_arbzeit_erfassen_postopen_2
// @description: Customization authrization of beas
// @usage: Select currenct user scrap right, if no right this tab will be disable.
select count(*) from [@ZMIAT]  t0 inner join [@ZMIA1]  t1 on t0.docentry=t1.docentry where U_Userid='<currentuser>' and t1.U_Tabcode='18'
if <wert1> n= 0 then
	dw_1.item.menge_schlecht_rm_t.visible=false
	dw_1.item.menge_schlecht_rm.visible=false
	dw_1.item.c_menge_schlecht_rm.visible=false
end if
// @usage: Select currenct user cost center right, if no right this tab will be disable.
select count(*) from [@ZMIAT]  t0 inner join [@ZMIA1]  t1 on t0.docentry=t1.docentry where U_Userid='<currentuser>' and t1.U_Tabcode='19'
if <wert1> n= 0 then
	dw_1.item.kstst_id.visible=false
	dw_1.item.kstst_id_t.visible=false
	dw_1.item.kstst_id_p.visible=false
  dw_1.item.kstst_id1_t.visible=false
end if

// @codeid: beas_fert_arbzeit_erfassen_postopen_3
// @description: Disable update "resource" field
item.aplatz_id=protect=1
item.aplatz_id_p=protect=1
item.aplatz_id_p.visible=0

// @codeid: beas_fert_arbzeit_erfassen_postopen_4
// @description: Disable update "cost element" field
item.timetype_id=protect=1
item.timetype_id_p.visible=0
end event

// @eventid: beas_fert_arbzeit_erfassen_itemchange_barcode
// @description: Tigger this event when change the barcode
// Add dropdown list to "serial number" field
windowevent itemchange barcode

// Add dropdown list to "serial number" field
item.udf1.dropdown.activate
//item.udf1.edit.update=no

// entsprechenden Timetype abhängig vom Typ hinterlegen
// 17.3.09 M.Heigl
setvar=wert1=
if <typ> = H then
  select "thtimetype_id" into timetype_id from "BEAS_FTAPL" where "nummer"=N'<dw_1.item.barcode.value>'
end if
if <typ> = N then
  select "tntimetype_id" into timetype_id from "BEAS_FTAPL" where "nummer"=N'<dw_1.item.barcode.value>'
end if
if <typ> = R then
  select "trtimetype_id" into timetype_id from "BEAS_FTAPL" where "nummer"=N'<dw_1.item.barcode.value>'
end if
if <typ> = A or <typ> = E  then
  select "timetype_id" into timetype_id from "BEAS_FTAPL" where "nummer"=N'<dw_1.item.barcode.value>'
end if
menu=redraw
end event

// @eventid: beas_fert_arbzeit_erfassen_click_udf1_p
// @description: Refresh the dropdown list of Serial Number, when click the dropdown button
windowevent click udf1_p
// @test: item.udf1.dropdown.select item from beas_ftpos_reservierung &
//  where belnr_id=<dw_1.item.belnr_id.value,#0> and belpos_id=<dw_1.item.belpos_id.value,#0>
select udf2 from beas_ftpos &
where beas_ftpos.belnr_id=<belnr_id> and beas_ftpos.belpos_id=<belpos_id>
setvar=l_sn_comp_item=<wert1>

item.udf1.dropdown.select X.serialnum &
//sum(X.ibt1_quantity) &
from ( &
select case when oitm.mansernum='Y' then osrn.distnumber else obtn.distnumber end as 'serialnum'&
//,oitm.itemcode+'' as 'ibt1_itemcode' &
//,oitm.itemname+'' as 'oitm_itemname' &
//,oitm.mansernum+'' as mansernum &
//,oitm.manbtchnum+'' as manbtchnum &
,itl1.quantity as 'ibt1_quantity' &
//, ige1.u_posid+0 as ign1_u_posid &
from ign1  &
inner join oitl on oitl.doctype=59 and oitl.docentry=ign1.docentry and oitl.docline=ign1.linenum  &
inner join itl1 on itl1.logentry=oitl.logentry  &
left join obtn on obtn.itemcode=itl1.itemcode and obtn.sysnumber=itl1.sysnumber  &
left join osrn on osrn.itemcode=itl1.itemcode and osrn.sysnumber=itl1.sysnumber  &
inner join oitm on oitm.itemcode=itl1.itemcode  &
left join beas_ftzuordnung serial on ign1.U_belnrid = serial.belnr_id  &
and ign1.U_belposid = serial.belpos_id and serial.pos_id = ign1.U_posid  &
and serial.from_item = osrn.DistNumber and oitl.docentry = serial.basedocentry  &
and oitl.DocLine = serial.baseline &
left join beas_ftzuordnung batch on ign1.U_belnrid = batch.belnr_id  &
and ign1.U_belposid = batch.belpos_id and batch.pos_id = ign1.U_posid  &
and batch.from_item = obtn.DistNumber and oitl.docentry = batch.basedocentry  &
and oitl.DocLine = batch.baseline &
where ign1.u_belnrid=<belnr_id> and ign1.u_belposid=<belpos_id> &
and oitm.itemcode=N'<l_sn_comp_item>' and oitm.ManSerNum='Y' &
union all &
select case when oitm.mansernum='Y' then osrn.distnumber else obtn.distnumber end as 'serialnum'&
,itl1.quantity as 'ibt1_quantity' &
from ige1  &
inner join oitl on oitl.doctype=60 and oitl.docentry=ige1.docentry and oitl.docline=ige1.linenum  &
inner join itl1 on itl1.logentry=oitl.logentry  &
left join obtn on obtn.itemcode=itl1.itemcode and obtn.sysnumber=itl1.sysnumber  &
left join osrn on osrn.itemcode=itl1.itemcode and osrn.sysnumber=itl1.sysnumber  &
inner join oitm on oitm.itemcode=itl1.itemcode  &
left join beas_ftzuordnung serial on ige1.U_belnrid = serial.belnr_id  &
and ige1.U_belposid = serial.belpos_id and serial.pos_id = ige1.U_posid  &
and serial.from_item = osrn.DistNumber and oitl.docentry = serial.basedocentry  &
and oitl.DocLine = serial.baseline &
left join beas_ftzuordnung batch on ige1.U_belnrid = batch.belnr_id  &
and ige1.U_belposid = batch.belpos_id and batch.pos_id = ige1.U_posid  &
and batch.from_item = obtn.DistNumber and oitl.docentry = batch.basedocentry  &
and oitl.DocLine = batch.baseline &
where ige1.u_belnrid=<belnr_id> and ige1.u_belposid=<belpos_id> &
and oitm.itemcode=N'<l_sn_comp_item>' and oitm.ManSerNum='Y' &
) X &
group by X.serialnum &
having sum(X.ibt1_quantity)=-1
end event

// @eventid: beas_fert_arbzeit_erfassen_click_b_3
// @description: If click the “partial receipt” button (none final completion), Yield qty + Scrap qty + Produced yield qty + As yet scrap qty must be less and equal as Planned Qty
windowevent click b_3
if <ctyp> =  then
  select <sollmenge> - (<gutmenge>+<schlechtmenge>+<menge_gut_rm>+<menge_schlecht_rm>)
// @test: messagebox=Test for "7.2.2.1	Time receipts validation when click “partial receipt” button" <cr_lf> &
//    Planned Qty: <sollmenge> <cr_lf> &
//    Completed Yield Qty: <gutmenge> <cr_lf> &
//    Completed Scrap Qty: <schlechtmenge> <cr_lf> &
//    Yield Qty: <menge_gut_rm> <cr_lf> &
//    Scrap Qty: <menge_schlecht_rm> <cr_lf> &
//    Avaliable Qty: <wert1>
  if <wert1> n< 0 then
    messagebox=error$beas_tr_1009$Partial time receipt error, Yield qty + Scrap qty + Produced yield qty + As yet scrap qty must be less and equal as Planned Qty!
    return false
  end if
end if
end event

// @eventid: beas_fert_arbzeit_erfassen_click_b_4
// @description: If click the “comfirm completion” button (final completion), Yield qty + Scrap qty + Produced yield qty + As yet scrap qty must be equal as Planned Qty
windowevent click b_4
if <ctyp> =  then
  select <sollmenge> - (<gutmenge>+<schlechtmenge>+<menge_gut_rm>+<menge_schlecht_rm>)
// @test: messagebox=Test for "7.2.2.2	Time receipts validation when click “comfirm completion” button <cr_lf> &
//    Planned Qty: <sollmenge> <cr_lf> &
//    Completed Yield Qty: <gutmenge> <cr_lf> &
//    Completed Scrap Qty: <schlechtmenge> <cr_lf> &
//    Yield Qty: <menge_gut_rm> <cr_lf> &
//    Scrap Qty: <menge_schlecht_rm> <cr_lf> &
//    Avaliable Qty: <wert1>
  if <wert1> n<> 0 then
    messagebox=error$beas_tr_1010$Final time confirm error, Yield qty + Scrap qty + Produced yield qty + As yet scrap qty must be equal as Planned Qty!
    return false
  end if
end if
end event

// @eventid: beas_fert_arbzeit_erfassen_click_pers_id_p
// @description: Person dropdown list can be refresh by time type, need to define the login time type of person first 
windowevent click pers_id_p
// @test: R=zeittyp_tr // setup
// A=zeittyp_te // process
// N=zeittyp_th // qc
// H=zeittyp_tn // rework
// item.pers_id.dropdown.select=pers_id,name2+' '+name1 from beas_pers where zeittyp_tr=1 and zeittyp_te=1 and zeittyp_th=1 and zeittyp_tn=1 
if <typ> = H then
  item.pers_id.dropdown.select=pers_id,name2+' '+name1 from beas_pers where zeittyp_th=1
end if
if <typ> = N then
  item.pers_id.dropdown.select=pers_id,name2+' '+name1 from beas_pers where zeittyp_th=1
end if
if <typ> = R then
  item.pers_id.dropdown.select=pers_id,name2+' '+name1 from beas_pers where zeittyp_tr=1
end if
if <typ> = A or <typ> = E  then
  item.pers_id.dropdown.select=pers_id,name2+' '+name1 from beas_pers where zeittyp_te=1
end if
end event
