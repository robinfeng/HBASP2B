// Coding ID: beas_qs_ftmessung_browse_postopen
// By: robin.feng@tech-sonic.net
// Date: 20140725
// Description: S/N can be entered by sample in QC order for S/N managed item
windowevent postopen

setglobal=g_sn_error=Y
setglobal=g_sn_errormessage=

select mansernum from oitm &
  where itemcode=N'<parent.dw_1.item.itemcode.value>'
if <wert1> = Y then
visible=udf1=1
protect=udf1=0
setglobal=g_sn_man=Y
else
visible=udf1=0
protect=udf1=1
setglobal=g_sn_man=N
end if

visible=distnumber=1
protect=distnumber=1
//create=button=name=del_no_sn,menutext=Del no S/N Sample,text=Del no S/N Sample

end event

//windowevent click del_no_sn
//sql=delete from beas_qsftmessung where charge_id=N'<str_parm.s_parm1>' and isnull(udf1,'')=''
//sql=delete from beas_qsftpos where charge_id=N'<str_parm.s_parm1>' and isnull(udf1,'')=''
//retrieve
//end event

// Coding ID: beas_qs_ftmessung_browse_itemchange_udf1
// By: robin.feng@tech-sonic.net
// Date: 20140725
// Description: Check the udf1(Serial Number):
// 1. S/N entered cannot be duplicated in one QC order
// 2. S/N entered must be within original document (GRPO)
// 3. S/N entered cannot be updated unless the measurement position of the sample has no value
//    Note: Only "Open" status sample can ernter S/N
windowevent itemchange udf1
//messagebox=<item.udf1.value> <cr_lf> &
//  <dw_1.item.udf1:[dwo-row].value> <cr_lf> &
//  <dw_1.item.pruefkz:[dwo-row].value> <cr_lf> &
//  <global:g_sn_error>

// Record the old serial number into "l_udf1"
select isnull(udf1,'') from beas_qsftmessung &
  where charge_id=N'<dw_1.item.charge_id.value>' and knd_id='<dw_1.item.knd_id:[dwo-row].value>'

setvar=l_udf1=<wert1>

// 1. S/N entered cannot be duplicated in one QC order
select count(*) from beas_qsftmessung &
  where charge_id=N'<dw_1.item.charge_id.value>' &
  and isnull(udf1,'')=N'<dw_1.item.udf1:[dwo-row].value>' &
  and isnull(udf1,'')<>''
if <wert1> n> 0 then

setglobal=g_sn_error=beas_qc_1006
setglobal=g_sn_errormessage=S/N entered cannot be duplicated in one QC order.
goto=end
end if

// 3. S/N entered cannot be updated unless the measurement position of the sample has no value
//    Note: Only "Open" status sample can ernter S/N

select count(1) from beas_qsftpos &
  where charge_id=N'<dw_1.item.charge_id.value>' and knd_id=N'<dw_1.item.knd_id:[dwo-row].value>' &
  and len(messwertc)>0
if <wert1> n> 0 and <global:g_basetyp> n= 20 then

setglobal=g_sn_error=beas_qc_1006
setglobal=g_sn_errormessage=S/N entered cannot be updated unless the measurement position of the sample has no value.
goto=end
end if

if <dw_1.item.pruefkz:[dwo-row].value> = F then
setglobal=g_sn_error=beas_qc_1006
setglobal=g_sn_errormessage=This sample status is Unlocked, you can't update this serial number.
goto=end
end if

if <dw_1.item.pruefkz:[dwo-row].value> = E then
setglobal=g_sn_error=beas_qc_1006
setglobal=g_sn_errormessage=This sample status is Blocked, you can't update this serial number.
goto=end
end if

// 2. S/N entered must be within original document (GRPO)
setglobal=g_basetyp=
if <parent.dw_1.item.typ.value> = W then
  setglobal=g_basetyp=20
end if

if <parent.dw_1.item.typ.value> = M then
  setglobal=g_basetyp=59
end if

setvar=wert1=
select mansernum from oitm &
  where itemcode=N'<parent.dw_1.item.itemcode.value>'

//message=<parent.dw_1.item.typ.value><cr_lf><wert1>
// If goods receipt an item with serial number management
if <wert1> = Y and <global:g_basetyp> n= 20 and <item.udf1.value> <> then
select count(1) from beas_qsfthaupt t1, oitl t2, itl1 t3, osrn t4 &
  where t1.basetype=<global:g_basetyp> and t1.charge_id=N'<dw_1.item.charge_id.value>' &
  and t1.basedocentry=t2.docentry and t1.baselinenum=t2.docline &
  and t2.logentry=t3.logentry &
  and t4.itemcode=N'<parent.dw_1.item.itemcode.value>' &
  and t4.sysnumber=t3.sysnumber &
  and t4.distnumber=N'<dw_1.item.udf1:[dwo-row].value>'

if <wert1> n= 0 then
setglobal=g_sn_error=beas_qc_1001
setglobal=g_sn_errormessage=The Serial Number is error, can't find in goods receipt serial number list(OSRN).
goto=end
end if
end if

[end]
//messagebox=<global:g_sn_error>
if <global:g_sn_error> = Y then
message=info$beas_qc_1002$The entried Serial Number is ok.
return true
else
setitem=udf1=<l_udf1>
message=error$<global:g_sn_error>$<global:g_sn_errormessage>
return false
end if
end event

// Coding ID: beas_qs_ftmessung_browse_b_1
// By: Robin
// Date: 20140815
// Description: 
// 1. S/N is empty, can't open measurement screen
// 2. S/N can not find in orginal document
// 3. If find duplicate S/N, can't open measurement screen
//windowevent click b_1
//// 1. S/N is empty, can't open measurement screen
//if <dw_1.item.udf1.value> =  then
//message=error$$S/N is empty, can't open measurement screen.
//return false
//end if
//
//if <global:g_sn_error> = N and <global:g_basetyp> n= 20 then
//messagebox=error$$Can't modify the measurement position, please check the serial number.
//return false
//end if
//end event

// Coding ID: beas_qs_ftmessung_browse_doubleclick
// By: Robin
// Date: 20140815
// Description: 
// 1. S/N is empty, can't open measurement screen
// 2. S/N can not find in orginal document
// 3. If find duplicate S/N, can't open measurement screen

windowevent doubleclick
// 1. S/N is empty, can't open measurement screen
if <global:g_sn_man> = Y and <dw_1.item.udf1.value> =  then
message=error$beas_qc_1013$S/N is empty, can't open measurement screen.
return false
end if

if <global:g_sn_man> = Y and <global:g_sn_error> = N and <global:g_basetyp> n= 20 then
messagebox=error$beas_qc_1014$Can't modify the measurement position, please check the serial number.
return false
end if

end event

windowevent click udf1
if <item.udf1.value> = then
item.udf1.value=<dw_1.item.distnumber.value>
end if
end event
