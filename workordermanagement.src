// @file: workordermanagement
// @author: Robin Feng
// @remarks: 
// @version: beas 9.0-000-002-036
// @path: N/A
// Modified: 2014.7 - 2014.10
// Some work order transation will tigger these function in be.as.
// Copyright(C) TechSonic
// All rights reserved.

// @functionid: beas_workordermanagement_timerecordingstart
// @description: This validation for time receipt:
// 1. S/N is mandatory if resource is defined as S/N relevant
// 2. S/N value entry must be one of the S/N of the preforms issued to the WO
// 3. Time confirmation can only be saved when the cost element is not empty
// 4. If the S/N is entered, the confirmed quantity cannot be more than 1
// 5. Disable quantity confirmation for time type setup
// 6. The From date and valuation date must be same
// 7. Validate the login time type of person and this time receipt’s time type are match or not
function timerecordingstart
if <sys_dwname> = fert_arbzeit_erfassen then
// @codeid: beas_workordermanagement_timerecordingstart_6
// @description: The From date and valuation date must be same
	if <anfzeit> <> <docdate> then
		messagebox=error$beas_tr_1007$The "From Date" and the "Valuation Date" must be same.
		return false
	end if

// @codeid: beas_workordermanagement_timerecordingstart_1
// @description: S/N is mandatory if resource is defined as S/N relevant
	// messagebox=<menge_gut_rm> <cr_lf> <menge_schlecht_rm> <cr_lf> <typ>
	setvar=wert1=
	//select aplatz_id,udf1 from beas_arbzeit where belnr_id=<e_belnr_id> and belpos_id=<e_belpos_id> and pos_id=<e_pos_id>
	setvar=l_aplatz=<aplatz_id>
	setvar=l_serialnumber=<udf1>
	setvar=l_sn_len=%len(<udf1>)

	//messagebox=<aplatz_id>,<udf1>

	select isnull(p_serialnumtag,'N') from beas_aplatz where aplatz_id=N'<l_aplatz>'
	setvar=res_sn_tag=<wert1>

	setvar=wert1=
	//select count(1) from beas_ftpos_reservierung &
	//  where belnr_id=<dw_1.item.belnr_id.value,#0> and belpos_id=<dw_1.item.belpos_id.value,#0> &
	//  and item='<dw_1.item.udf1.value>'
	//setvar=sn_in_reservation=<wert1>
	//messagebox=<inputserialnumber>
	if <res_sn_tag> = 'Y' and <l_sn_len> n= 0 then
		message=error$beas_tr_1001$The resource have to record serial number in time receipt, please input Serial Number
		return false
	end if

// @codeid: beas_workordermanagement_timerecordingstart_2
// @description: S/N value entry must be one of the S/N of the preforms issued to the WO
	select udf2 from beas_ftpos &
	where beas_ftpos.belnr_id=<e_belnr_id> and beas_ftpos.belpos_id=<e_belpos_id>
	setvar=l_sn_comp_item=<wert1>

	if <res_sn_tag> = 'Y' and <l_sn_comp_item> <>  then
		select count(1) &
		from ige1  &
		inner join oitl on oitl.doctype=60 and oitl.docentry=ige1.docentry and oitl.docline=ige1.linenum  &
		inner join itl1 on itl1.logentry=oitl.logentry  &
		left join obtn on obtn.itemcode=itl1.itemcode and obtn.sysnumber=itl1.sysnumber  &
		left join osrn on osrn.itemcode=itl1.itemcode and osrn.sysnumber=itl1.sysnumber  &
		inner join oitm on oitm.itemcode=itl1.itemcode  &
		left join beas_ftzuordnung serial on ige1.U_belnrid = serial.belnr_id  &
		and ige1.U_belposid = serial.belpos_id and serial.pos_id = ige1.U_posid  &
		and serial.from_item = osrn.DistNumber and oitl.docentry = serial.basedocentry  &
		and oitl.DocLine = serial.baseline &
		left join beas_ftzuordnung batch on ige1.U_belnrid = batch.belnr_id  &
		and ige1.U_belposid = batch.belpos_id and batch.pos_id = ige1.U_posid  &
		and batch.from_item = obtn.DistNumber and oitl.docentry = batch.basedocentry  &
		and oitl.DocLine = batch.baseline &
		where ige1.u_belnrid=<e_belnr_id> and ige1.u_belposid=<e_belpos_id> &
		and oitm.itemcode=N'<l_sn_comp_item>' and oitm.ManSerNum='Y' &
		and osrn.distnumber='<l_serialnumber>'
		if <wert1> n= 0 and <res_sn_tag> = Y then
		  message=error$beas_tr_1003$The Serial Number is error, can't find the s/n in current work order related serial management item goods issue.
		  return false
		end if
	end if

	//if <res_sn_tag> = 'Y' and <sn_in_reservation> n= 0 then
	//message=error$beas_tr_1002$The Serial Number is error, can't find the s/n in current work order s/n reservation list.
	//return false
	//end if

// @codeid: beas_workordermanagement_timerecordingstart_3
// @description: Time confirmation can only be saved when the cost element is not empty
	//messagebox=<timetype_id>
	if <timetype_id> = then
		message=error$beas_tr_1004$The cost element is mandatory.
		return false
	end if
	//return false

// @codeid: beas_workordermanagement_timerecordingstart_4
// @description: If the S/N is entered, the confirmed quantity cannot be more than 1
	setvar=qty=%numadd(<menge_gut_rm>,<menge_schlecht_rm>)
	if <l_sn_len> n> 0 and <qty> n> 1 then
		message=error$beas_tr_1005$You enter the S/N, therefore the confirmed quantity cannot be more than 1 
		return false
	end if

// @codeid: beas_workordermanagement_timerecordingstart_5
// @description: Disable quantity confirmation for time type setup

	// Note: 
	// If "Setup" and "Process" set cost element and the time type Setup quantity must be zero
	// messagebox=<barcode>,<trtimetype_id>,<timetype_id>
	select len(trtimetype_id), len(timetype_id) from beas_ftapl where nummer='<barcode>'
	if <typ> = R and <qty> n> 0 and <wert1> n> 0  and <wert2> n> 0 then
		message=error$beas_tr_1006$The time type is "Setup", therefore cannot input the quantity.
		return false
	end if

// @codeid: beas_workordermanagement_timerecordingstart_7
// @description: Validate the login time type of person and this time receipt’s time type are match or not
	select zeittyp_tr, zeittyp_te, zeittyp_th, zeittyp_tn into tr, te, th, tn &
	  from beas_pers where pers_id=N'<pers_id>'

	select typname into typname from beas_timeoperator where typ=N'<typ>'
	// messagebox=<pers_id>, <typ>, <typname> <cr_lf> &
	// tr=<tr>, te=<te>, th=<th>, tn=<tn>
	if <typ> = H and <tn> n<> 1  then
	  messagebox=error$beas_tr_1011$This person didn't have <typname> time type right
	  return false
	end if
	if <typ> = N and <th> n<> 1 then
	  messagebox=error$beas_tr_1011$This person didn't have <typname> time type right
	  return false
	end if
	if <typ> = R and <tr> n<> 1 then
	  messagebox=error$beas_tr_1011$This person didn't have <typname> time type right
	  return false
	end if
	if <typ> = A and <te> n<> 1  then
	  messagebox=error$beas_tr_1011$This person didn't have <typname> time type right
	  return false
	end if
end if

if <sys_dwname> = fert_arbzeit_erfassen_cost then
	messagebox=TODO: cost adjust <cr_lf> Please contect to your administrator!
	return false
end if
end function