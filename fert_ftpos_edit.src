// @file: fert_ftpos_edit
// @author: Robin Feng
// @remarks: 
// @version: beas 9.0-000-002-036
// @path: BEAS > Production > Work Orders > Work Order position
// Modified: 2014.7 - 2014.10
// be.as work order position window will tigger this customize code.
// Copyright(C) TechSonic
// All rights reserved.

// @eventid: beas_fert_ftpos_edit_itemchange_scraptag
// @descraption: Update WIA when click scrap order tag on work order position screen
windowevent itemchange scraptag

// Define the scrap order wip variance account
select u_parameter1 from [@zc001] where u_variablename=N'Z_SCRAP_Account'

setvar=scrap_acct_wip_var=<wert1>
select gel_menge * isnull(me_umr,1) from beas_ftpos &
  where belnr_id=<str_parm.belnr_id,#0> and belpos_id=<str_parm.belpos_id,#0>
setvar=complete_quantity=<wert1>
if <dw_1.item.scraptag.value> = 'Y' and <complete_quantity> n= 0 then
	dw_1.item.account_variance.value=<scrap_acct_wip_var>
end if

if <dw_1.item.scraptag.value> = 'Y' and <complete_quantity> n<> 0 then
	message=error$wo_1001$The finish goods was receipted, can not scrap this work order.
	setitem=scraptag='N'
	return false
end if

if <dw_1.item.scraptag.value> = 'N' then
	// WIP-Konto der Baugruppe
	sbodiapi=getaccount=typ=wipacct<tab>itemcode=<itemcode><tab>whscode=<whscode><tab>source=fert_ftpos_edit.src.itemcode.itemchanged
	setitem=account_wip=<value>
	select case when isnull(u_beas_wip,'N') in ('J','Y','T','1') then 'Y' else 'N'  end from oact where acctcode='<account_wip>'
	if <wert1> <> Y then
	   messagebox=fehler$ftposstd02355$Das angegebene WIA-Konto ist nicht als WIA-Konto definiert! Bitte Datenprüfung starten und korrigieren!
	end if
	// WIP-Difference der Baugruppe
	sbodiapi=getaccount=typ=wipvaracct<tab>itemcode=<itemcode><tab>whscode=<whscode><tab>source=fert_ftpos_edit.src.itemcode.itemchanged
	setitem=account_variance=<value>
end if
end event

// @eventid: beas_fert_ftpos_edit_open
// @description: 
// 1. Create a scrap tag for scrap order, locate the field in this screen
// 2. Rename the fields on this screen
windowevent open
// @codeid: beas_fert_ftpos_edit_open_1
// @description: Create a scrap tag for scrap order, locate the field in this screen
setvar=ll_x=<item.startzeit_struktur.x>
setvar=ll_y=<item.startzeit_struktur.y>

create=text=x=3100,y=<ll_y>,width=700,text=Scrap order
create=checkbox=x=3400,y=<ll_y>,id=userfield1,name=scraptag,tab=41
select p_scraptag into scraptag from beas_ftpos &
  where belnr_id=<str_parm.belnr_id,#0> and belpos_id=<str_parm.belpos_id,#0>
//select u_D_sn_comp_item,u_M_cmp_version into udf2,udf3 from oitm where itemcode=N'<itemcode>'
// @codeid: beas_fert_ftpos_edit_open_2
// @description: Rename the fields on this screen
[automaticscript]
dw_1.item.u_znr_t.formatstring=text=Drawing version,
[/automaticscript]
[automaticscript]
dw_1.item.lieferdatum_t.formatstring=text=Finish date,
[/automaticscript]
end event

// @eventid: beas_fert_ftpos_edit_update
// @description: Update "scraptag" value to database
windowevent update
sql=update beas_ftpos set p_scraptag='<dw_1.item.scraptag.value>' &
//  udf2='<udf2>', udf3='<udf3>' &
  where belnr_id=<str_parm.belnr_id,#0> &
  and belpos_id=<str_parm.belpos_id,#0>
end event

// @eventid:beas_fert_ftpos_edit_itemchange_itemcode
// @description: Copy the CMP_Version, serial component itemcode from item master data
windowevent itemchange itemcode
select u_D_sn_comp_item,u_M_cmp_version from oitm where itemcode=N'<itemcode>'
setitem=udf2=<wert1>
setitem=udf3=<wert2>
end event
