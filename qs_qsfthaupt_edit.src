// @file: qs_qsfthaupt_edit
// @author: Robin Feng
// @remarks: 
// @version: beas 9.0-000-002-036
// @path: BEAS > Quality control > QC orders -> QC order
// Modified: 2014.7 - 2014.10
// be.as QC order window will tigger this customize code.
// Copyright(C) TechSonic
// All rights reserved.

// @eventid: beas_qs_qsfthaupt_edit_postopen
// @description: Hide the "input by test" tab, when the is serial management item.
windowevent postopen
select mansernum from oitm where itemcode=N'<dw_1.item.itemcode.value>'
if <wert1> = Y then
	tab=protect=4=1
end if
end event

// @eventid: beas_qs_qsfthaupt_edit_itemchange_gutmenge
// @description: 
// If the QC warehouse cost indicator is "1-Must be zero", then default unlock 
// warehouse is consignment warehouse linked to this QC warehouse, which means 
// the warehouse cost indicator is ‘1-Must be zero’, and the consignment vendor 
// in warehouse definition is same as the QC warehouse, and the warehouse type 
// defined in BEAS is blank.

// If the QC warehouse cost indicator is "3-Not must be zero",
// then default unlock warehouse is the default warehouse of this item
windowevent itemchange gutmenge

if <dw_1.item.basetype.value> n= 20 then
	select isnull(u_m_warehousecost,3), u_m_consivendor into l_whcost, l_consivendor &
	 	from owhs &
		where whscode=N'<whscode>'

	if <l_whcost> n= 1 then
		select whscode from owhs &
			where isnull(u_m_warehousecost,3)=1 &
			and u_m_consivendor = N'<l_consivendor>' &
			and len(u_beas_lck)=0
		setitem=gutwhscode=<wert1>	
	end if

	if <l_whcost> n= 3 then
		select DfltWH from oitm where itemcode=N'<itemcode>'
		setitem=gutwhscode=<wert1>	
	end if
end if
end event

// @eventid: beas_qs_qsfthaupt_edit_itemchange_schlechtmenge
// @description: 
// If the QC warehouse cost indicator is "1-Must be zero", then default lock warehouse
// is return consignment warehouse linked to this QC warehouse, which means the warehouse
// cost indicator is ‘1-Must be zero’, and the consignment vendor in warehouse definition
// is same as the QC warehouse, and the warehouse type defined in BEAS is ‘Blocked warehouse’.
// If the QC warehouse cost indicator is "3-Not must be zero", then default lock warehouse is 920.

windowevent itemchange schlechtmenge
if <dw_1.item.basetype.value> n= 20 then
	select isnull(u_m_warehousecost,3), u_m_consivendor into l_whcost, l_consivendor &
	 	from owhs &
		where whscode=N'<whscode>'
	if <l_whcost> n= 1 then
		select whscode from owhs &
			where isnull(u_m_warehousecost,3)=1 &
			and u_m_consivendor = N'<l_consivendor>' &
			and isnull(u_beas_lck,'')='Y'
		setitem=schlechtwhscode=<wert1>
	end if

	if <l_whcost> n= 3 then
		setitem=schlechtwhscode=920
	end if
end if
end event